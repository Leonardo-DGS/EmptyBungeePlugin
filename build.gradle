import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'net.leonardo_dgs'
version = '1.0.0'
description = ''

String mainPackage = project.group + '.' + project.name.toLowerCase()
String author = 'Leonardo_DGS'
String website = ''

String bungeeVersion = '1.15-SNAPSHOT'
String lombokVersion = '1.18.10'
String bStatsVersion = '1.5'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    shadow 'net.md-5:bungeecord-api:' + bungeeVersion
    shadow 'net.md-5:bungeecord-chat:' + bungeeVersion
    shadow 'net.md-5:bungeecord-event:' + bungeeVersion
    shadow 'org.projectlombok:lombok:' + lombokVersion
    annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
    implementation 'org.bstats:bstats-bungeecord-lite:' + bStatsVersion
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "name"       : project.name,
            "mainPackage": mainPackage,
            "version"    : project.version,
            "description": project.description,
            "author"     : author,
            "website"    : website
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = project.name + '.jar'
    minimize()
    relocate 'org.bstats.bungeecord', mainPackage + '.bstats'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}
